#import "standard.fx";
#import "redmath.fx";
#import "redwindows.fx";
#import "redopengl.fx";
#import "redallocators.fx";

using standard::memory::allocators::stdheap;
using standard::system::windows;
using standard::math;

// ============================================================================
// C runtime FFI
// ============================================================================
extern
{
    def !!
        Sleep(u32) -> void;
};

// ============================================================================
// MESH TYPES
// ============================================================================

#def MAX_VERTS 8192;
#def MAX_FACES 16384;

struct Vec3 { float x, y, z; };
struct Face { int a, b, c;   };

struct Mesh
{
    Vec3* verts;
    Face* faces;
    int   vert_count;
    int   face_count;
};

// ============================================================================
// OBJ LOADER
// ============================================================================

def load_obj(byte* path, Mesh* mesh) -> bool
{
    print("Loading object ...\n\0");
    noopstr f = "\\Users\\kvthw\\Flux\\examples\\Lowpoly_Notebook_2.obj\0";
    int size = get_file_size(f);
    if (size <= 0)
    {
        print("Error: file not found or empty.\n\0");
        return false;
    };
    byte* buffer = fmalloc((u64)size + 1);
    if (buffer == 0)
    {
        print("Error: out of memory.\n\0");
        return false;
    };
    int bytes_read = read_file(f, buffer, size);
    if (bytes_read <= 0)
    {
        print("Error: could not read file.\n\0");
        free(buffer);
        return false;
    };
    buffer[bytes_read] = (byte)0;
    print("File loaded, initializing arrays...\n\0");

    mesh.verts      = (Vec3*)fmalloc((u64)MAX_VERTS * 12);
    mesh.faces      = (Face*)fmalloc((u64)MAX_FACES * 12);
    mesh.vert_count = 0;
    mesh.face_count = 0;

    byte[512] line;
    int pos = 0;
    print("Before loop...\n\0");

    while (pos < bytes_read)
    {
        print("In loop..\n\0");
        int line_len = 0;
        while (pos + line_len < bytes_read & buffer[pos + line_len] != '\n' & line_len < 511)
        {
            byte ch = buffer[pos + line_len];
            if (ch != '\r')
            {
                line[line_len] = ch;
                line_len = line_len + 1;
            };
            pos = pos + 1;
        };
        line[line_len] = (byte)0;
        if (pos < bytes_read) { pos = pos + 1; };

        if (line[0] == 'v' & line[1] == ' ')
        {
            if (mesh.vert_count < MAX_VERTS)
            {
                float vx = 0.0;
                float vy = 0.0;
                float vz = 0.0;
                sscanf(line, "v %f %f %f\0", @vx, @vy, @vz);
                mesh.verts[mesh.vert_count].x = vx;
                mesh.verts[mesh.vert_count].y = vy;
                mesh.verts[mesh.vert_count].z = vz;
                mesh.vert_count = mesh.vert_count + 1;
            };
        };

        if (line[0] == 'f' & line[1] == ' ')
        {
            if (mesh.face_count < MAX_FACES)
            {
                int fa = 0;
                int fb = 0;
                int fc = 0;
                sscanf(line, "f %d %d %d\0", @fa, @fb, @fc);
                mesh.faces[mesh.face_count].a = fa - 1;
                mesh.faces[mesh.face_count].b = fb - 1;
                mesh.faces[mesh.face_count].c = fc - 1;
                mesh.face_count = mesh.face_count + 1;
            };
        };
    };

    free(buffer);
    return true;
};

// ============================================================================
// 3D MATH
// ============================================================================

def rotate_x(Vec3* v, float s, float c) -> Vec3
{
    Vec3 r;
    r.x = v.x;
    r.y = v.y * c - v.z * s;
    r.z = v.y * s + v.z * c;
    return r;
};

def rotate_y(Vec3* v, float s, float c) -> Vec3
{
    Vec3 r;
    r.x =  v.x * c + v.z * s;
    r.y =  v.y;
    r.z = (v.x * (0.0 - s)) + v.z * c;
    return r;
};

def project(Vec3* v, int cx, int cy, float fov, float cam_z) -> POINT
{
    float dz = v.z + cam_z;
    POINT p;
    if (dz < 0.001)
    {
        p.x = cx;
        p.y = cy;
        return p;
    };
    p.x = cx + (int)(v.x * fov / dz);
    p.y = cy - (int)(v.y * fov / dz);
    return p;
};

// ============================================================================
// MAIN
// ============================================================================

def main(int argc, byte** argv) -> int
{
    Mesh mesh;
    bool loaded;

    const int LINE_WIDTH = 2,     // Pixels
              SLEEP_MS   = 5,
              WIN_WIDTH  = 1280,
              WIN_HEIGHT = 1024,
              RED =   0,
              GREEN = 0,
              BLUE =  255;

    const float CAM_FOV  = 360.0,
                CAM_Z    = 2.5;     // Distance from origin

    if (argc == 2)
    {
        loaded = load_obj(@argv[1], @mesh);
    }
    else
    {
        print("Flux 3D Wireframe Renderer (.OBJ)\nUsage: wireframe <file>.obj\n\0");
        loaded = false;
    };
    Window win("Flux 3D Wireframe\0", WIN_WIDTH, WIN_HEIGHT, CW_USEDEFAULT, CW_USEDEFAULT);
    SetForegroundWindow(win.handle);

    if (!loaded)
    {
        print("Falling back to spinning cube ...\n\0");
        // Fallback: spinning cube
        mesh.vert_count = 8;
        mesh.face_count = 12;
        mesh.verts      = (Vec3*)fmalloc((u64)8  * 12);
        mesh.faces      = (Face*)fmalloc((u64)12 * 12);

        mesh.verts[0].x = -1.0; mesh.verts[0].y = -1.0; mesh.verts[0].z = -1.0;
        mesh.verts[1].x =  1.0; mesh.verts[1].y = -1.0; mesh.verts[1].z = -1.0;
        mesh.verts[2].x =  1.0; mesh.verts[2].y =  1.0; mesh.verts[2].z = -1.0;
        mesh.verts[3].x = -1.0; mesh.verts[3].y =  1.0; mesh.verts[3].z = -1.0;
        mesh.verts[4].x = -1.0; mesh.verts[4].y = -1.0; mesh.verts[4].z =  1.0;
        mesh.verts[5].x =  1.0; mesh.verts[5].y = -1.0; mesh.verts[5].z =  1.0;
        mesh.verts[6].x =  1.0; mesh.verts[6].y =  1.0; mesh.verts[6].z =  1.0;
        mesh.verts[7].x = -1.0; mesh.verts[7].y =  1.0; mesh.verts[7].z =  1.0;

        mesh.faces[0].a  = 0; mesh.faces[0].b  = 1; mesh.faces[0].c  = 2;
        mesh.faces[1].a  = 0; mesh.faces[1].b  = 2; mesh.faces[1].c  = 3;
        mesh.faces[2].a  = 4; mesh.faces[2].b  = 6; mesh.faces[2].c  = 5;
        mesh.faces[3].a  = 4; mesh.faces[3].b  = 7; mesh.faces[3].c  = 6;
        mesh.faces[4].a  = 0; mesh.faces[4].b  = 3; mesh.faces[4].c  = 7;
        mesh.faces[5].a  = 0; mesh.faces[5].b  = 7; mesh.faces[5].c  = 4;
        mesh.faces[6].a  = 1; mesh.faces[6].b  = 5; mesh.faces[6].c  = 6;
        mesh.faces[7].a  = 1; mesh.faces[7].b  = 6; mesh.faces[7].c  = 2;
        mesh.faces[8].a  = 0; mesh.faces[8].b  = 4; mesh.faces[8].c  = 5;
        mesh.faces[9].a  = 0; mesh.faces[9].b  = 5; mesh.faces[9].c  = 1;
        mesh.faces[10].a = 3; mesh.faces[10].b = 2; mesh.faces[10].c = 6;
        mesh.faces[11].a = 3; mesh.faces[11].b = 6; mesh.faces[11].c = 7;
    };

    POINT* proj = (POINT*)fmalloc((u64)mesh.vert_count * 16);

    float angle_x = 0.0;
    float angle_y = 0.0;
    float fov     = CAM_FOV;
    float cam_z   = CAM_Z;
    int   cx      = WIN_WIDTH / 2;
    int   cy      = WIN_HEIGHT / 2;

    enum dc_enum
    {
        BLACK,
        RED,
        GREEN,
        BLUE
    };

    //dc_enum dce;

    DWORD[4] draw_colors = [RGB(  0, 0, 0), // Black
                            RGB(255, 0, 0), // Red
                            RGB(0, 255, 0), // Green
                            RGB(0, 0, 255)  // Blue
                           ];

    DWORD draw_color = draw_colors[dc_enum.BLUE];
    DWORD bg_color = draw_colors[dc_enum.BLACK];

    float sx  = 0.0;
    float cxr = 0.0;
    float sy  = 0.0;
    float cyr = 0.0;
    int i  = 0;
    int fi = 0;
    int a  = 0;
    int b  = 0;
    int fv = 0;
    Vec3 v;
    Vec3 rx;
    Vec3 ry;
    Canvas c(win.handle, win.device_context);

    while (win.process_messages())
    {
        sx  = sin(angle_x);
        cxr = cos(angle_x);
        sy  = sin(angle_y);
        cyr = cos(angle_y);

        i = 0;
        while (i < mesh.vert_count)
        {
            v.x = mesh.verts[i].x;
            v.y = mesh.verts[i].y;
            v.z = mesh.verts[i].z;
            rx = rotate_x(@v,  sx,  cxr);
            ry = rotate_y(@rx, sy,  cyr);
            proj[i] = project(@ry, cx, cy, fov, cam_z);
            i = i + 1;
        };

        c.clear(bg_color);
        c.set_pen(draw_color, LINE_WIDTH);

        fi = 0;
        while (fi < mesh.face_count)
        {
            a  = mesh.faces[fi].a;
            b  = mesh.faces[fi].b;
            fv = mesh.faces[fi].c;
            c.line(proj[a].x,  proj[a].y,  proj[b].x,  proj[b].y);
            c.line(proj[b].x,  proj[b].y,  proj[fv].x, proj[fv].y);
            c.line(proj[fv].x, proj[fv].y, proj[a].x,  proj[a].y);
            fi = fi + 1;
        };

        angle_x = angle_x + 0.014;
        angle_y = angle_y + 0.020;

        if (angle_x > PIF) { angle_x = angle_x - 2.0 * PIF; };
        if (angle_y > PIF) { angle_y = angle_y - 2.0 * PIF; };

        Sleep(SLEEP_MS);
    };

    free(proj);
    free(mesh.verts);
    free(mesh.faces);

    win.__exit();

    return 0;
};
